ext {
  interlokParentGradle = project.findProperty('interlokParentGradle') ?: 'https://raw.githubusercontent.com/adaptris-labs/interlok-build-parent/master/build.gradle'
  interlokVersion = project.findProperty('interlokVersion') ?: '4.0-SNAPSHOT'
  interlokMavenLocal = project.findProperty('interlokMavenLocal') ?: false
  interlokInstallDirectory = project.findProperty('interlokInstallDirectory') ?: "${buildDir}/distribution"
  buildEnv = project.findProperty('buildEnv') ?: "dev"
}

repositories {
  if (interlokMavenLocal) {
    mavenLocal()
  }
}

allprojects {
  apply from: "${interlokParentGradle}"
}

dependencies {
  def interlokDeps = ["json", "apache-http", "service-tester", "aws-s3", "aws-sqs", "aws-kinesis-sdk", "expressions", "flyway", "csv-json", "filesystem"] as String[]
  interlokDeps.each {
    add("interlokRuntime",  [group: "com.adaptris", name: "interlok-${it}", version: interlokVersion, changing: true])
    add("interlokJavadocs", [group: "com.adaptris", name: "interlok-${it}", version: interlokVersion, changing: true, classifier: "javadoc", transitive: false])
  }
}

installDist {
  destinationDir = new File(interlokInstallDirectory)
}

task cleanInstall () {
  description 'Delete install directory'
  group 'build'
  doLast {
    new File(interlokInstallDirectory).deleteDir()
  }
}

installDist.dependsOn interlokVersionReport
